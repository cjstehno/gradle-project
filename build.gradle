plugins {
    id 'com.github.hierynomus.license' version '0.13.1'
    id 'com.github.kt3k.coveralls' version '2.8.1'
    id 'com.gradle.plugin-publish' version '0.9.7'
    id 'java-gradle-plugin'
    id 'groovy'
    id 'jacoco'
    id 'maven-publish'
    id 'com.stehno.gradle.webpreview' version '0.2.0'
    id 'org.asciidoctor.convert' version '1.5.3'
    id 'com.stehno.gradle.site' version '0.0.1' // this should be the deployed version
}

version = '0.0.1'
group = 'com.stehno.gradle'

sourceCompatibility = 8
targetCompatibility = 8

repositories {
    jcenter()
}

configurations {
    groovyDoc
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    // TODO: figure out how to pull the version # from the Gradle distro
    groovyDoc "org.codehaus.groovy:groovy-groovydoc:2.4.7"
    groovyDoc "org.codehaus.groovy:groovy-ant:2.4.7"

    compile('io.github.http-builder-ng:http-builder-ng-core:0.14.0') {
        exclude module: 'groovy-all'
    }

    testCompile 'junit:junit:4.12'
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testCompile('com.stehno.ersatz:ersatz:1.1.0') {
        exclude module: 'groovy-all'
    }
}

groovydoc {
    groovyClasspath = configurations.groovyDoc
}

asciidoctor {
    options doctype: 'book'

    backends = ['html5']

    attributes 'source-highlighter': 'coderay',
        'coderay-linenums-mode': 'table',
        icon: 'font',
        linkattrs: true,
        encoding: 'utf-8'
}

license {
    header rootProject.file('license_header.txt')
    ext.name = 'Christopher J. Stehno'
    ext.email = 'chris@stehno.com'
    ext.year = Calendar.instance.get(Calendar.YEAR)
}

pluginBundle {
    website = 'http://cjstehno.github.io/gradle-site/'
    vcsUrl = 'https://github.com/cjstehno/gradle-site'
    description = 'Gradle plugin to help create and manage a project documentation site.'
    tags = ['gradle', 'groovy']

    plugins {
        webpreviewPlugin {
            id = 'com.stehno.gradle.site'
            displayName = 'Gradle Site Plugin'
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

task publishSite(type: GradleBuild, group: 'Publishing', description: 'Publishes the documentation web site.', dependsOn: ['site']) {
    buildFile = 'publish.gradle'
    tasks = ['publishGhPages']
}

tasks.site.dependsOn = ['build', 'jacocoTestReport', 'groovydoc', 'asciidoctor']
